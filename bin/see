#!/bin/zsh
set -e

h_mpv() { exec mpv --no-terminal --force-window $1 }

if [[ $# != 1 ]]; then
	echo "Usage: $0 <file>" >&2
	exit 1
fi

error()
{
	echo $1 >&2
	notify-send $1
	exit 1
}

unhandled() { error "No handler for $1" }

arg=$1
if [[ $arg =~ '^(\w+):' ]]; then
	case $match[1] in
		http(|s)|ftp)
			exec ${=BROWSER:-firefox} $arg
			;;
		file)
			arg=$arg[8,-1]
			;;
		*)
			unhandled $arg
			;;
	esac
fi

if [[ ! -e $arg ]]; then
	error "No such file or directory $arg (in $PWD)"
fi

mime=$(file -Lb --mime-type $arg)
case $mime in
	application/pdf)
		#exec llpp $arg
		exec zathura $arg
		;;
	application/postscript) ;&
	image/vnd.djvu)
		exec zathura $arg
		;;
	application/epub+zip)
		exec ebook-viewer $arg
		;;
	image/(|x-(|*-))(bmp|gif|jp(|e)g|png|tiff))
		#exec sxiv -a $arg
		#exec feh $arg
		exec nomacs $arg
		;;
	image/svg+xml)
		exec inkview $arg
		;;
	audio/*) ;&
	video/*) ;&
	application/vnd.rn-realmedia)
		h_mpv $arg
		;;
	application/(msword|vnd.ms-(excel|powerpoint))) ;&
	application/vnd.oasis.opendocument.*)
		exec libreoffice $arg
		;;
	text/html)
		exec ${=BROWSER:-firefox} $arg
		;;
	text/plain)
		exec gedit --class=__floating $arg
		;;
	inode/directory)
		exec nautilus $arg
		;;
esac

case $arg:e:l in
	wav|flac|mp3|ogg|wmw|tta|wv|mpc)
		h_mpv $arg
		;;
	(doc|xls|ppt)(|x))
		exec libreoffice $arg
		;;
	epub|mobi)
		exec ebook-viewer $arg
		;;
esac

unhandled "$PWD/$arg ($mime)"
