#!/usr/bin/env perl
use warnings; use strict;

=head1 SYNOPSIS

  $ base
  Usage: base NUM [[{b|o|d|x}]{b|o|d|x}[0][width]]
  
  $ base 35 b
  100011
  
  $ base 35 b0
  0b100011
  
  $ base 35 b08
  0b00100011
  
  $ base 35 b8 
  00100011
  
  $ base a xd 
  10
  
  $ base xa d 
  10
  
  $ base 0xa d 
  10
  
  $base 127 x0
  0x7f
  
  $base 127 X0
  0X7F
  
  $ base 17 x
  11
  
  $ base 17 ox
  f
  
  $ base 111 x
  6f
  
  $ base 111 bx
  7
  
  $ base b111 x 
  7
  
=cut

if (@ARGV == 1 || (@ARGV == 2 && $ARGV[1] =~ /^([bodx])?(?:([bodx])(0)?)(\d+)?$/i)) {
	my ($from, $to, $prefix, $width) = ($1 || '', $2 || 'd', defined $3 ? '#' : '', $4 || '');
	my $val = $ARGV[0];
	my $dec;

	if ($from eq 'b' || $val =~ /^0?b/i) {
		$val =~ s/^(0?b)?/b/ni;
		$dec = oct($val);
	}
	elsif ($from eq 'x' || $val =~ /^0?x/i) {
		$dec = hex($val);
	}
	elsif ($from eq 'o' || $val =~ /^0/) {
		$dec = oct($val);
	}
	else {
		$dec = int($val);
	}

	if ($width && $prefix && ($to eq 'b' || $to eq 'x')) {
		$width += 2;
	}

	printf("%${prefix}0$width$to\n", $dec);
}
else {
	die "Usage: $0 NUM [[{b|o|d|x}]{b|o|d|x}[0][width]]\n";
}
